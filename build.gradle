plugins {
    id 'java'
    id 'jacoco'
    id 'maven'
    id 'net.researchgate.release' version '2.8.0'
    id 'org.sonarqube' version '2.7'
}

group = 'org.github.rjo1970'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
    compile.transitive = false
    compileClasspath.transitive = false
    deployerJars
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'javax.activation:activation:1.1.1'
    testCompile 'javax.mail:mail:1.4.7'

    deployerJars 'org.apache.maven.wagon:wagon-ssh:3.3.2'
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.setDeprecation true
}

project.tasks['sonarqube'].dependsOn tasks.withType(Test)
sonarqube {
    properties {
        property 'sonar.tests', ['src/test/java']
        property 'sonar.junit.reportPaths', ['build/test-results/test']
        property 'sonar.surefire.reportsPath', ['build/test-results/test']
        property 'sonar.jacoco.reportPaths', ['build/jacoco/test.exec']
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "${abc_artifactory_contextUrl}/gradle-release-local") {
                authentication(userName: "${abc_artifactory_user}", password: "${abc_artifactory_password}")
            }
            snapshotRepository(url: "${abc_artifactory_contextUrl}/gradle-dev-local") {
                authentication(userName: "${abc_artifactory_user}", password: "${abc_artifactory_password}")
            }
        }
    }
}

release {
    buildTasks = ['assemble']
    tagTemplate = '$name-$version'
    git {
        requireBranch = 'master'
    }
}

afterReleaseBuild.dependsOn uploadArchives
